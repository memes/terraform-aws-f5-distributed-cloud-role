variable "name" {
  type = string
  validation {
    condition     = var.name == null ? true : can(regex("^[a-z][a-z0-9-]{0,17}[a-z0-9]$", var.name))
    error_message = "The prefix variable must be RFC1035 compliant and between 1 and 19 characters in length."
  }
  description = <<-EOD
  The prefix to use when naming resources.
  EOD
}

variable "tags" {
  type = map(string)
  validation {
    condition     = var.tags == null ? true : length(compact([for k, v in var.tags : can(regex("^[a-zA-Z0-9][a-z0-9+-=.,_:@]{0,128}$", k)) && can(regex("^(?:[A-Za-z0-9][-A-Za-z0-9_.]{0,254})?[A-Za-z0-9]$", v)) ? "x" : ""])) == length(keys(var.tags))
    error_message = "Each tag key:value pair must match expectations."
  }
  default     = {}
  description = <<-EOD
  An optional set of key:value string pairs that will be added to generated
  resources.
  EOD
}

variable "path" {
  type    = string
  default = "/"
  # validation {
  #   condition     = var.path == null || var.title == "" ? true : length(var.title) <= 100
  #   error_message = "The title variable must be empty, or up to 100 characters long."
  # }
  description = <<-EOD
  The path in which to create IAM policy. Default is '/'.
  EOD
}

variable "description" {
  type        = string
  default     = null
  description = <<-EOD
  The optional description to assign to the custom policy role. If left blank
  (default), a suitable description will be created.
  EOD
}

variable "members" {
  type    = list(string)
  default = []
  validation {
    condition     = length(join("", [for member in var.members : can(regex("^(group|serviceAccount|user):[^@]+@[^@]+$", member)) ? "x" : ""])) == length(var.members)
    error_message = "Each member value must be a fully-qualified IAM email address. E.g. serviceAccount:foo@project.iam.gserviceaccount.com."
  }
  description = <<-EOD
  An optional list of accounts that will be assigned the custom role. Default is
  an empty list.
  EOD
}

variable "name_prefix" {
  type    = string
  default = "f5_xc"
  validation {
    condition     = can(regex("^[a-z0-9_.]{3,59}$", var.name_prefix))
    error_message = "The name_prefix variable must be between 3 and 59 characters in length and only contain alphanumeric, underscore and periods."
  }
  description = <<-EOD
  The prefix to use when generating random identifier for the new policy if
  `name` field is blank. The default is 'f5_xc' which will generate a unique role
  identifier of the form 'f5_xc_XXXX', where XXXX is a generated by AWS.
  EOD
}
